plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'me.chrommob'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = uri("https://repo.opencollab.dev/main/")
    }
    maven {
        url 'https://repo.codemc.io/repository/maven-releases/'
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly "dev.folia:folia-api:1.20.4-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.11.5'
    implementation 'com.github.retrooper.packetevents:spigot:2.2.1'
    implementation 'com.github.ChromMob:ConfigManager:9d04690e35'
    implementation 'com.zaxxer:HikariCP:5.1.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate("me.chrommob.config", "me.chrommob.libs.config")
    relocate("com.github.retrooper.packetevents", "me.chrommob.libs.packetevents")
    relocate("com.zaxxer.hikari", "me.chrommob.libs.hikaricp")
}
